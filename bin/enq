#! /usr/bin/env perl

use v5.12;
use warnings;

use FindBin;
use local::lib $FindBin::Bin =~ s{/bin$}{/local-lib}r;
use lib $FindBin::Bin =~ s{/bin$}{/lib}r;

use Getopt::Long;

use Cwd qw/ abs_path /;

use JCC::Queue::Schema;

my $schema = JCC::Queue::Schema->connect(qq{dbi:SQLite:dbname=$ENV{HOME}/queue.db});

GetOptions(
    'c|category=s' => \my $category,
    'a|allow-duplicates' => \my $allow_duplicates,
) or die "Error in command line arguments";

if (@ARGV) {
    enqueue($_) for @ARGV;
} else {
    while (<>) {
        chomp;
        enqueue($_);
    }
}

sub enqueue {
    my ($url) = @_;

    if ($url !~ m/^https?:/) {
        $url = abs_path($url);
    }

    return unless $url;

    my $method = $allow_duplicates ? 'create' : 'find_or_create';

    $schema->resultset('Item')->$method({
        category => $category,
        url => $url,
    });
}
